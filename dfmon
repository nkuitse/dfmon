#!/bin/zsh -e

integer err=0
typeset host=$(hostname -s); host=$host:l
typeset var=/var/local/dfmon/$host
typeset timestamp=$(print -P '%D{%Y%m%dT%H%M%S}')
typeset used mount

show-version() {
    cat <<EOS
dfmon __VERSION__ by __AUTHOR__
__COPYRIGHT__
Licensed under the terms of the GNU General Public License, version 2.
See LICENSE for details.
EOS
    exit 0
}

main() {
    typeset trend opt all=false last=false reset=false verbose=false
    while getopts :alrvV opt; do
        case $opt in
            (a) all=true ;;
            (l) last=true ;;
            (r) reset=true ;;
            (v) verbose=true ;;
            (V) show-version ;;
        esac
    done
    if $last; then
        print '+/- %Now %Prv Mountpoint' >&2
    fi
    df-summary | while read -r used mount; do
        mkdir -p $var$mount
        cd $var$mount 
        [[ ! -e SKIP ]] || continue
        cur=${used[1,-2]}
        if [[ ! -e LOG ]]; then
            ! $verbose || print "$host: disk usage on $mount is now being monitored: currently $used used" >&2
            $last || print $timestamp $cur >> LOG
            continue
        fi
        prev=$(tail -n1 < LOG | awk '{ print $2 }')
        if $last; then
            if (( cur > prev )); then
                trend='+'$( printf '%2d' $(( cur - prev )) )
            elif (( prev > cur )); then
                trend='-'$( printf '%2d' $(( prev - cur )) )
            fi
            printf '%-3.3s %4d %4d %s\n' "$trend" $cur $prev $mount >&2
            continue
        fi
        print $timestamp $cur >> LOG
        if $reset; then
            ! $verbose || print "$host: resetting baseline on $mount" >&2
            continue
        elif [[ -z $prev ]]; then
            ! $verbose || print "$host: setting baseline on $mount" >&2
            continue
        elif [[ -e THRESHOLD.percent ]]; then
            threshold=$(cat THRESHOLD.percent)
        elif [[ -e THRESHOLD.increase ]]; then
            threshold=$(( prev + $(cat THRESHOLD.increase) ))
        else
            threshold=$(( prev + 1 ))
        fi
        if (( cut > threshold )); then
            print "$host: disk usage on $mount exceeds threshold (was $prev% now $cur% used)"
            err=2
        fi
    done
    exit $err
}

df-summary() {
    if $all; then
        df -lP | awk '/ \// { print $5, $6 }'
    else
        df -lP | awk '/^\// { print $5, $6 }' 
    fi
}

main "$@"
